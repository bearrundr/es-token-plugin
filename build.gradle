/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

buildscript {
    repositories {
        if (System.getProperty("repos.mavenlocal") != null) {
            // with -Drepos.mavenlocal=true we can force checking the local .m2 repo which is useful for building against
            // elasticsearch snapshots
            mavenLocal()
        }
        mavenCentral()
        maven {
            name 'sonatype-snapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        // Version of elasticsearch
        classpath "org.elasticsearch.gradle:build-tools:${elasticsearchVersion}"
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    description 'PMML Plugin'
    classname 'org.elasticsearch.plugin.TokenPlugin'
}

// Version of the plugin
version = "${pluginVersion}"

ext.compactProfile = 'full'

dependencies {
    // Not really used at the moment - just to show how to include dependencies
    compile "org.jpmml:pmml-agent:${jpmmlVersion}"
    compile "org.jpmml:pmml-model:${jpmmlVersion}"
    compile "org.jpmml:pmml-schema:${jpmmlVersion}"
}

test {
    systemProperty 'tests.security.manager', 'false'
}

compileJava.options.compilerArgs << "-Xlint:-deprecation"

// TODO: temporary workaround until it's fixed in elasticsearch
thirdPartyAudit.enabled = false

forbiddenPatterns {
    // it really doesn't like tabs in knime workspace and log files
    exclude 'knime/knime_workspace/**/*'
    exclude 'demo/**/*'
}

integTest {
    cluster {
        setting 'script.engine.pmml_model.stored', 'true'
    }
}

run {
    setting 'script.engine.pmml_model.stored', 'true'
}
